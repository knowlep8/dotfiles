#!zsh

local -a args
local cvs_cmd=$argv[(r)^(-*)]

local HOME=~kwf

case "$cvs_cmd" in
  st*)
    $HOME/lib/perl/cvs-status.pl "$@"
    ;;

  di*)
    $HOME/lib/perl/cvs-diff.pl "$@"
    ;;

  copy)
    $HOME/lib/perl/cvs-copy.pl $argv[2,-1]
    ;;

  up*)
    local line
    local -a conflicts

    command cvs "$@" |& while read line; do
        case "$line" in
          (cvs update: Updating *)
            print -nP "\r%EExamining ${line#cvs update: Updating }"
            ;;

          (cvs update: * is no longer in the repository)
            print -P "\r%ED ${${line#cvs update: }% is no longer in the repository}"
            ;;

          (C *)
            conflicts=($conflicts $line)
            ;&

          *)
            print -P "\r%E$line"
            ;;
        esac
    done

    local rc=$pipestatus[0]
    
    if [ -n "$conflicts" ]; then
        print -P "\r%EConflicted files:"
        print -l $conflicts
        if [ $rc -eq 0 ]; then
            return 32
        else
            return $rc
        fi
    else
        return $rc
    fi
    ;;

  *)
    command cvs "$@"
    ;;
esac
